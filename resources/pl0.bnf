<program> = block <"."> .

block = [ const_decl ]
        [ var_decl ]
        { proc_decl }
        statement .

const_decl = <"const"> ident "=" number {<","> ident "=" number} <";"> .

var_decl = <"var"> ident {<","> ident} <";"> .

proc_decl = <"procedure"> ident [<"("> ident {<","> ident} <")">] <";"> block <";"> .

statement = [ ident ":=" expression
            | "call" ident [<"("> expression {<","> expression} <")">]
            | "?" ident
            | "!" expression
            | "begin" statement { <";"> statement } <"end">
            | "if" condition <"then"> statement 
            | "while" condition <"do"> statement
            ].

condition = "odd" expression
          | expression ("="|"<>"|"#"|"<"|"<="|">"|">=") expression .

expression = term { ("+"|"-") term }.

term = factor {("*"|"/") factor}.

factor = ["+"|"-"] factor | ident | number | string | <"("> expression <")"> .

ident = #"[A-Za-z_][0-9A-Za-z_]*".

number = #"[0-9]+".

string = #"'[^']*'"
